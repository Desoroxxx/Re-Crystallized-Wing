import org.jetbrains.gradle.ext.Gradle

plugins {
    id("java")
    id("java-library")
    id("org.jetbrains.gradle.plugin.idea-ext") version "1.1.7"
    id("com.matthewprenger.cursegradle") version "1.4.0"
    id("net.neoforged.gradle") version "6.+"
    id("io.freefair.lombok") version "8.3"
    id("com.github.gmazzo.buildconfig") version "4.1.2"
    id("org.parchmentmc.librarian.forgegradle") version '1.+'
}

// Add version to the jar name
version project.version

// Set the toolchain version to decouple the Java we run Gradle with from the Java used to compile and run the mod
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
        vendor.set(JvmVendorSpec.ADOPTIUM)
    }
    // Generate sources jar when building and publishing
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.fork = true
}

minecraft {
    mappings channel: 'parchment', version: '2022.11.27-1.19.2'

    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"

            property "forge.logging.console.level", "debug"

            mods {
                recrystallizedwing {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"

            property "forge.logging.console.level", "debug"

            mods {
                recrystallizedwing {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"

            property "forge.logging.console.level", "debug"

            args "--mod", "recrystallizedwing", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            mods {
                recrystallizedwing {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }

repositories {
    maven {
        name = "Forge Maven"
        url = "https://maven.minecraftforge.net/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.19.2-43.2.23"
}

buildConfig {
    buildConfigField("String", "ID", "\"${project.id}\"")
    buildConfigField("String", "NAME", "\"${project.name}\"")
    buildConfigField("String", "VERSION", "\"${project.version}\"")
}

processResources {
    inputs.property "id", project.id
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") { fcd ->
        include "META-INF/mods.toml"
        fcd.expand (
                "id": project.id,
                "version": project.version
        )
    }
}

idea {
    module {
        inheritOutputDirs = true
        excludeDirs = [
                file(".settings"),
                file(".github"),
                file(".gradle"),
                file("gradle"),
                file(".idea"),
                file("build"),
                file("run"),
                file("bin")
        ]
    }
}
